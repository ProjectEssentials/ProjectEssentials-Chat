buildscript {
    repositories {
        maven { url("https://files.minecraftforge.net/maven") }
        maven { url("https://libraries.minecraft.net") }
        maven { url("https://plugins.gradle.org/m2/") }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath(
            group: "net.minecraftforge.gradle",
            name: "ForgeGradle",
            version: forgeGradleVersion,
            changing: true
        )
        classpath(
            "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        )
        classpath(
            "org.jetbrains.kotlin:kotlin-serialization:$kotlinVersion"
        )
    }
}

apply(plugin: "net.minecraftforge.gradle")
apply(plugin: "kotlin")
apply(plugin: "java")
apply(plugin: "kotlinx-serialization")

version = projectEssentialsVersion
group = "com.mairwunnx.$projectEssentialsId"
archivesBaseName = projectEssentialsName

minecraft {
    mappings(
        channel: mappingsChannelType,
        version: mappingsChannelVersion
    )

    runs {
        client {
            workingDirectory(project.file(devRunClientPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentialsChat {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory(project.file(devRunServerPath))
            property("forge.logging.markers", "SCAN,REGISTRIES,REGISTRYDUMP")
            property("forge.logging.console.level", "debug")
            mods {
                ProjectEssentialsChat {
                    source(sourceSets.main)
                }
            }
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url("https://minecraft.curseforge.com/api/maven/") }
    maven { url("https://libraries.minecraft.net") }
    maven { url("https://jitpack.io") }
}

dependencies {
    minecraft(group: "net.minecraftforge", name: "forge", version: forgeBaseVersion)
    compile(group: "com.mojang", name: "brigadier", version: brigadierBaseVersion)
    compile(
        group: "org.jetbrains.kotlinx",
        name: "kotlinx-serialization-runtime",
        version: kotlinxSerializationVersion
    )
    compile(
        group: "org.jetbrains.kotlin",
        name: "kotlin-stdlib-$kotlinJdkVersionTarget",
        version: kotlinVersion
    )
    compile(
        group: "com.github.projectessentials",
        name: "ProjectEssentials-Permissions",
        version: "v1.14.4-0.2.0.0"
    )
    compile(
        group: "com.github.projectessentials",
        name: "ProjectEssentials-Core",
        version: "v1.14.4-1.+"
    )
    compile(
        group: "com.github.projectessentials",
        name: "ProjectEssentials-Cooldown",
        version: "v1.14.4-1.+"
    )
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : projectEssentialsName,
            "Specification-Vendor"    : projectEssentialsVendor,
            "Specification-Version"   : projectEssentialsVersion,
            "Implementation-Title"    : projectEssentialsName,
            "Implementation-Version"  : projectEssentialsVersion,
            "Implementation-Vendor"   : projectEssentialsVendor,
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourceCompatibility = targetCompatibility =
    compileJava.sourceCompatibility =
        compileJava.targetCompatibility = jvmVersionTarget

compileKotlin.kotlinOptions.jvmTarget =
    compileTestKotlin.kotlinOptions.jvmTarget = jvmVersionTarget

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
}
