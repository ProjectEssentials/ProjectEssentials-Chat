buildscript {
    repositories {
        maven { url 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.72'
}

apply(plugin: "net.minecraftforge.gradle")

String packagesToken = new String(package_public_token.decodeBase64())

version = module_version
group = "com.projectessentials.chat"
archivesBaseName = module_name

minecraft {
    accessTransformer = file 'src/main/resources/META-INF/accesstransformer.cfg'
    mappings channel: forge_mappings_channel_type, version: forge_mappings_channel_version
}

repositories {
    maven {
        name = package_maven_repo_name
        url = uri "$package_maven_repo-core"
        credentials {
            username = package_maven_repo_mail
            password = packagesToken
        }
    }
    maven { url 'https://libraries.minecraft.net' }
    mavenCentral()
    jcenter()
}

dependencies {
    minecraft "net.minecraftforge:forge:$forge_version"
    implementation("com.projectessentials.core:ProjectEssentials-Core:$core_version") {
        transitive = false
    }
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-$kotlin_jdk_version_target:$kotlin_version"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : module_name,
                "Specification-Vendor"    : module_vendor,
                "Specification-Version"   : module_version,
                "Implementation-Title"    : module_name,
                "Implementation-Version"  : module_version,
                "Implementation-Vendor"   : module_vendor,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

sourceCompatibility = targetCompatibility =
        compileJava.sourceCompatibility =
                compileJava.targetCompatibility = project_jvm_version_target

compileKotlin.kotlinOptions.jvmTarget =
        compileTestKotlin.kotlinOptions.jvmTarget = project_jvm_version_target

